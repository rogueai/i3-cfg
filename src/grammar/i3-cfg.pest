config = {
     SOI
     ~ ((mode_block | stmt | unknown)? ~ NEWLINE)*
     ~ EOI
}
unknown = _{(!stmt ~ !NEWLINE ~ ANY)+}
mode_block = _{ "mode" ~ "\"" ~ ( !"\"" ~ !NEWLINE ~ ANY )+ ~ "\"" ~ "{"~ ( !"}" ~ ANY )* ~ "}" }
stmt = { expr }
expr = { keybinding }

keybinding = { bindmouse | bindsym | bindcode }

bindsym = { "bindsym" ~ "--release"? ~ binding_sym ~ "exec"? ~ "--no-startup-id"? ~ command }
bindcode = { "bindcode" ~ "--release"? ~ binding_code ~ "exec"? ~ "--no-startup-id"? ~ command }
bindmouse = { "bindsym" ~ "--release"? ~ "--border"? ~ "--whole-window"? ~ "--exclude-titlebar"? ~ binding_mouse ~ "exec"? ~ "--no-startup-id"? ~ command }

binding_sym = ${ (group ~ "+")? ~ (modifier ~ "+")* ~ keysym }
binding_code = ${ (group ~ "+")? ~ (modifier ~ "+")* ~ keycode }
binding_mouse = ${ (group ~ "+")? ~ (modifier ~ "+")* ~ "button" ~  button}

modifier = @{ ("Mod" ~ '1'..'5') | "Ctrl" | "Alt" | "Shift" }
group = @{ "Group" ~ '1'..'5' }
keysym =  @{(!ws ~ ANY)+}
keycode = @{(ASCII_DIGIT)+}
button = @{(ASCII_DIGIT)+}
command = { (!nl ~ ANY)+ }

ws = _{ WHITESPACE }
nl    = _{ "\n" | "\r\n" }

WHITESPACE = _{ " " | "\t" }
COMMENT    = _{ ("#" ~ (!NEWLINE ~ ANY)*) }
