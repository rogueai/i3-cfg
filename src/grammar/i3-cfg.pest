config = {
     SOI
     ~ ((mode_block | stmt | unknown)? ~ NEWLINE)*
     ~ EOI
}
unknown = _{(!stmt ~ !NEWLINE ~ ANY)+}
mode_block = _{ "mode" ~ "\"" ~ ( !"\"" ~ !NEWLINE ~ ANY )+ ~ "\"" ~ "{"~ ( !"}" ~ ANY )* ~ "}" }
stmt = { expr }
expr = { keybinding }

keybinding = { bindmouse | bindsym | bindcode }

bindsym = { "bindsym" ~ "--release"? ~ binding_sym ~ criteria? ~ command }
bindcode = { "bindcode" ~ "--release"? ~ binding_code ~ criteria? ~ command }
bindmouse = { "bindsym" ~ "--release"? ~ "--border"? ~ "--whole-window"? ~ "--exclude-titlebar"? ~ binding_mouse ~ criteria? ~ command }

binding_sym = ${ (group ~ "+")? ~ (modifier ~ "+")* ~ keysym }
binding_code = ${ (group ~ "+")? ~ (modifier ~ "+")* ~ keycode }
binding_mouse = ${ (group ~ "+")? ~ (modifier ~ "+")* ~ "button" ~  button}

modifier = @{ ("Mod" ~ '1'..'5') | "Ctrl" | "Alt" | "Shift" }
group = @{ "Group" ~ '1'..'5' }
keysym =  @{(!ws ~ ANY)+}
keycode = @{(ASCII_DIGIT)+}
button = @{(ASCII_DIGIT)+}

command = { command_name ~ (!nl ~ ANY)+? }
command_name = {
    "exec"
    | "split"
    | "layout"
    | "focus"
    | "move"
    | "swap"
    | "sticky"
    | "workspace"
    | "resize"
    | "mark"
    | "title_format"
    | "title_window_icon"
    | "border"
    | "shmlog"
    | "debuglog"
    | "restart"
    | "reload"
    | "exit"
    | "scratchpad"
    | "nop"
    | "bar"
}
criteria = { "[" ~ criterion ~ criterion* ~ "]" }
criterion = @{ criterion_name ~ "=" ~ "\"" ~ (!"\"" ~ ANY)+ ~ "\"" }
criterion_name = {
    "all"
    | "class"
    | "instance"
    | "window_role"
    | "window_type"
    | "machine"
    | "id"
    | "title"
    | "urgent"
    | "workspace"
    | "con_mark"
    | "con_id"
    | "floating"
    | "floating_from"
    | "tiling"
    | "tiling_from"
}

ws = _{ WHITESPACE }
nl = _{ "\n" | "\r\n" }

WHITESPACE = _{ " " | "\t" }
COMMENT    = _{ ("#" ~ (!NEWLINE ~ ANY)*) }
